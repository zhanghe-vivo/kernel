import("//build/toolchain/bluekernel.gni")
template("posix_testsuite_case") {
  # format: posixfunc_1_1
  forward_variables_from(invoker, "*")
  case_name = target_name
  exe_name = "librs_posix_image_${case_name}"
  runner_name = "librs_posix_runner_${case_name}"
  checker_name = "run_librs_posix_${case_name}"
  executable(exe_name) {
    testonly = true

    deps = [
      "//kernel/librs/tests/posixtestsuite:${case_name}",
      "//kernel/librs/tests/posixtestsuite:librs_for_posix_testsuite",
    ]
    inputs = [ "//kernel/kernel/src/bsp/$bsp/link.x" ]
    ldflags = [
      "-T",
      rebase_path("//kernel/kernel/src/bsp/$bsp/link.x"),
      "-Wl,-cref,-u,Reset_Handler",
      "-nostartfiles",
      "-lgcc",
      "-nodefaultlibs",
    ]
  }
  gen_qemu_runner(runner_name) {
    testonly = true
    img = ":${exe_name}"
    qemu = "$qemu_exe"
    board = "$bsp"
  }
  run_qemu_check(checker_name) {
    testonly = true
    runner = ":${runner_name}"
    checker = "//kernel/librs/tests/posixtestsuite/common_checker.test"
  }
  source_set(case_name) {
    name_components = string_split(case_name, "_")

    # filename format error, should be failed
    dir_name = name_components[0] + "_" + name_components[1]
    file_name = name_components[2] + "-" + name_components[3] + ".c"

    sources = [ "//external/posixtestsuite/conformance/interfaces/${dir_name}/${file_name}" ]

    include_dirs = [ "//kernel/librs/newlib/include" ]
    cflags = [
      "-std=gnu99",
      "-D_POSIX_C_SOURCE=200112L",
      "-O2",
      "-Wall",
      "-nostartfiles",
    ]
  }
}
