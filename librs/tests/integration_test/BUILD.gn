import("//build/templates/rust.gni")
import("//build/toolchain/bluekernel.gni")
import("//kernel/common_crate_rustflags.gni")

build_rust("librs_integration_test_image") {
  testonly = true
  crate_type = "bin"
  sources = [ "integration_test.rs" ]
  edition = "2021"

  proc_macro_deps = [ "//kernel/test_harness:bluekernel_test_macro" ]

  deps = [
    "//external/semihosting/v0.1.20:semihosting",
    "//external/spin/v0.9.8:spin",
    "//kernel/header:bluekernel_header",
    "//kernel/kernel:bluekernel",
    "//kernel/librs:librs_test",
    "//kernel/scal:bluekernel_scal_test",
    "//libc:libc",
  ]
  if (coverage || profile) {
    deps += [
      "//external/minicov/v0.3.7:minicov",
      "//external/semihosting/v0.1.20:semihosting",
    ]
  }
  inputs = [ "//kernel/kernel/src/boards/$board/link.x" ]
  rustflags = test_image_rustflags
}

gen_qemu_runner("librs_integration_test_runner") {
  testonly = true
  img = ":librs_integration_test_image"
  qemu = "$qemu_exe"
  machine = "$machine"
  semihosting = true
  if ("$board" == "qemu_virt64_aarch64") {
    qemu_args = "-cpu cortex-a53 -net none -chardev stdio,id=con,mux=on -serial chardev:con -mon chardev=con,mode=readline"
  }
}

run_qemu_check("run_librs_integration_test") {
  testonly = true
  runner = ":librs_integration_test_runner"
  if (coverage) {
    img = ":librs_integration_test_image"
    checker = "//kernel/kernel/src/cov.rs"
  } else {
    checker = "integration_test.rs"
  }
}
