import("//build/templates/build_template.gni")
import("//build/toolchain/blueos.gni")
import("//kernel/common_crate_rustflags.gni")

group("check_kernel") {
  testonly = true
  deps = [
    ":run_integration_test",
    ":run_unittest",
  ]
}

shared_deps = [
  "//external/bitflags/v2.9.0:bitflags",
  "//external/cfg-if/v1.0.0:cfg_if",
  "//external/const-default/v1.0.0:const_default",
  "//external/embedded-io/v0.6.1:embedded_io",
  "//external/log/v0.4.22:log",
  "//external/pinned-init/v0.0.8:pinned_init",
  "//external/rust-fatfs/v0.4:fatfs",
  "//external/safe-mmio/v0.2.5:safe_mmio",
  "//external/semihosting/v0.1.20:semihosting",
  "//external/spin/v0.9.8:spin",
  "//external/thiserror/v2.0.9:thiserror",
  "//external/tock-registers/v0.9.0:tock_registers",
  "//external/zerocopy/v0.8.25:zerocopy",
  "//kernel/header:blueos_header",
  "//kernel/infra:blueos_infra",
  "//kernel/kconfig:blueos_kconfig",
  "//libc:libc",
]
if (coverage || profile) {
  shared_deps += [
    ":atomic",
    "//external/minicov/v0.3.7:minicov",
    "//external/semihosting/v0.1.20:semihosting",
  ]
}

shared_rust_build_flags = []
if (direct_syscall_handler) {
  shared_rust_build_flags += [
    "--cfg",
    "direct_syscall_handler",
  ]
}

if (board == "qemu_mps2_an385" || board == "qemu_mps3_an547") {
  shared_rust_build_flags += [
    "--cfg",
    "hardware_schedule",
  ]
  shared_deps += [ "//external/cortex-m:cortex_m" ]
} else if (board == "qemu_virt64_aarch64") {
  shared_deps += [
    "//external/arm-gic/v0.4.0:arm_gic",
    "//external/flat_device_tree/v3.1.1:flat_device_tree",
    "//external/virtio-drivers/v0.11.0:virtio_drivers",
  ]
}

shared_rust_build_flags += [
  "--cfg",
  "target_board=\"$board\"",
]

build_rust("kernel_unittest") {
  testonly = true
  crate_name = "kernel_unittest"
  crate_type = "bin"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  proc_macro_deps = [
    "//external/paste/v1.0.15:paste",
    "//external/rust-delegate/v0.13.3:delegate",
    "//kernel/test_harness:blueos_test_macro",
  ]
  cfgs = blueos_default_cfgs
  deps = shared_deps
  configs += [ "//kernel/kconfig:kconfigs" ]
  inputs = [ "//kernel/kernel/src/boards/$board/link.x" ]
  rustflags = shared_rust_build_flags
  rustflags += test_image_rustflags
}

build_rust("kernel_integration_test") {
  testonly = true
  crate_type = "bin"
  sources = [ "tests/integration_test.rs" ]
  edition = "2021"
  proc_macro_deps = [ "//kernel/test_harness:blueos_test_macro" ]
  deps = [
    ":blueos",
    "//external/semihosting/v0.1.20:semihosting",
    "//external/smoltcp/v0.12.0:smoltcp",
    "//kernel/rsrt:rsrt",
    "//kernel/scal:blueos_scal_test",
    "//libc:libc",
  ]
  if (coverage || profile) {
    deps += [ "//external/minicov/v0.3.7:minicov" ]
  }
  cfgs = blueos_default_cfgs
  configs += [ "//kernel/kconfig:kconfigs" ]
  inputs = [ "//kernel/kernel/src/boards/$board/link.x" ]
  rustflags = test_image_rustflags
}

build_rust("blueos") {
  crate_type = "rlib"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  proc_macro_deps = [
    "//external/paste/v1.0.15:paste",
    "//external/rust-delegate/v0.13.3:delegate",
  ]
  cfgs = blueos_default_cfgs
  deps = shared_deps
  configs += [ "//kernel/kconfig:kconfigs" ]
  rustflags = shared_rust_build_flags
  rustflags += common_crate_rustflags
}

build_rust("blueos_std_static") {
  crate_name = "blueos"
  crate_type = "staticlib"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  proc_macro_deps = [
    "//external/paste/v1.0.15:paste",
    "//external/rust-delegate/v0.13.3:delegate",
  ]
  deps = shared_deps
  configs += [ "//kernel/kconfig:kconfigs" ]
  cfgs = blueos_default_cfgs
  rustflags = shared_rust_build_flags
  rustflags += common_crate_rustflags
  rustflags += [
    "--cfg",
    "std",
  ]
}

build_rust("blueos_std") {
  crate_name = "blueos"
  crate_type = "rlib"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  proc_macro_deps = [
    "//external/paste/v1.0.15:paste",
    "//external/rust-delegate/v0.13.3:delegate",
  ]
  deps = shared_deps
  cfgs = blueos_default_cfgs
  rustflags = shared_rust_build_flags
  rustflags += common_crate_rustflags
  rustflags += [
    "--cfg",
    "std",
  ]
}

build_rust("blueos_posixtestsuite") {
  testonly = true
  crate_name = "blueos"
  crate_type = "rlib"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  proc_macro_deps = [
    "//external/paste/v1.0.15:paste",
    "//external/rust-delegate/v0.13.3:delegate",
  ]
  deps = shared_deps
  cfgs = blueos_default_cfgs
  rustflags = shared_rust_build_flags
  rustflags += common_crate_rustflags
  rustflags += [
    "--cfg",
    "posixtestsuite",
  ]
}

cbindgen("rust_wrapper") {
  sources = [ "cbindgen.toml" ]
  args = [
           "--quiet",
           "-c",
         ] + rebase_path([ "cbindgen.toml" ], root_build_dir) +
         rebase_path([ "." ], root_build_dir)
}

gen_qemu_runner("integration_test_runner") {
  testonly = true
  semihosting = true
  img = ":kernel_integration_test"
  qemu = "$qemu_exe"
  machine = "$machine"
  if ("$board" == "qemu_virt64_aarch64") {
    qemu_args =
        "-cpu cortex-a53" + " -smp 4" + " -chardev stdio,id=con,mux=on" +
        " -serial chardev:con" + " -global virtio-mmio.force-legacy=false" +
        " -device virtio-rng-device,bus=virtio-mmio-bus.1" +
        " -mon chardev=con,mode=readline"
    block_img = "test.img"
    net_args = "-netdev user,id=u1,hostfwd=tcp:127.0.0.1:10024-10.0.2.15:23,hostfwd=tcp:127.0.0.1:15002-10.0.2.15:5001 -device virtio-net-device,netdev=u1,bus=virtio-mmio-bus.2"
  } else if ("$board" == "qemu_riscv64") {
    qemu_args = qemu_riscv64_run_args
  }
}

run_qemu_check("run_integration_test") {
  testonly = true
  runner = ":integration_test_runner"
  if (coverage) {
    img = ":kernel_integration_test"
    checker = "src/cov.rs"
  } else {
    checker = "tests/integration.checker"
  }
}

gen_qemu_runner("unittest_runner") {
  testonly = true
  img = ":kernel_unittest"
  qemu = "$qemu_exe"
  machine = "$machine"
  if ("$board" == "qemu_virt64_aarch64") {
    qemu_args =
        "-cpu cortex-a53" + " -smp 4" + " -chardev stdio,id=con,mux=on" +
        " -serial chardev:con" + " -global virtio-mmio.force-legacy=false" +
        " -device virtio-rng-device,bus=virtio-mmio-bus.1" +
        " -mon chardev=con,mode=readline"
    block_img = "test.img"
    net_args = "-netdev user,id=u1,hostfwd=tcp:127.0.0.1:10023-10.0.2.15:23,hostfwd=tcp:127.0.0.1:15001-10.0.2.15:5001 -device virtio-net-device,netdev=u1,bus=virtio-mmio-bus.2"
  } else if ("$board" == "qemu_riscv64") {
    qemu_args = qemu_riscv64_run_args
  }
  semihosting = true
}

run_qemu_check("run_unittest") {
  testonly = true
  runner = ":unittest_runner"
  if (coverage) {
    img = ":kernel_unittest"
    checker = "src/cov.rs"
  } else {
    checker = "src/unittests.checker"
  }
}

static_library("atomic") {
  sources = [ "src/sync/libatomic/atomic.c" ]
}
