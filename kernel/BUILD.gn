import("//build/templates/build_template.gni")
import("//kernel/common_crate_rustflags.gni")

build_rust("bluekernel") {
  crate_type = "rlib"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  deps = [
    "//external/cfg-if/v1.0.0:cfg_if",
    "//external/const-default/v1.0.0:const_default",
    "//external/log/v0.4.22:log",
    "//external/paste/v1.0.15:paste",
    "//external/pinned-init/v0.0.8:pinned_init",
    "//external/spin/v0.9.8:spin",
    "//kernel/arch:bluekernel_arch",
    "//kernel/header:bluekernel_header",
    "//kernel/infra:bluekernel_infra",
    "//kernel/kconfig:bluekernel_kconfig",
  ]
  features = [
    "tlsf",
    "overflow_check",
    "idle_hook",
    "heap",
    "heap_isr",
    "debugging_init",
    "event",
    "messagequeue",
    "mailbox",
    "mutex",
    "semaphore",
    "rwlock",
    "condvar",
    "thread_priority_max",
    "schedule_with_time_slice",
  ]

  rustflags = []
  if (direct_syscall_handler) {
    rustflags += [
      "--cfg",
      "direct_syscall_handler",
    ]
  }
  if (bsp == "qemu-mps2-an385" || bsp == "qemu-mps3-an547") {
    rustflags += [
      "--cfg",
      "hardware_schedule",
    ]
  }
  rustflags += common_crate_rustflags
}

cbindgen("rust_wrapper") {
  deps = [ ":bluekernel" ]
  sources = [ "cbindgen.toml" ]
  args = [
           "--quiet",
           "-c",
         ] + rebase_path([ "cbindgen.toml" ], root_build_dir) +
         rebase_path([ "." ], root_build_dir)
}
