import("//build/templates/build_template.gni")
import("//build/toolchain/bluekernel.gni")
import("//kernel/common_crate_rustflags.gni")

group("check_kernel") {
  testonly = true
  deps = [
    ":run_integration_test",
    ":run_unittest",
  ]
}

build_rust("kernel_integration_test") {
  testonly = true
  crate_type = "bin"
  sources = [ "tests/integration_test.rs" ]
  edition = "2021"
  proc_macro_deps = [ "//kernel/test_harness:bluekernel_test_macro" ]
  deps = [
    ":bluekernel",
    "//kernel/scal:bluekernel_scal_test",
    "//libc:libc",
  ]

  inputs = [ "//kernel/kernel/src/boards/$bsp/link.x" ]
  rustflags = test_image_rustflags
  rustflags += common_crate_rustflags
}

shared_deps = [
  "//external/bitflags/v2.9.0:bitflags",
  "//external/cfg-if/v1.0.0:cfg_if",
  "//external/const-default/v1.0.0:const_default",
  "//external/cortex-m:cortex_m",
  "//external/embedded-io/v0.6.1:embedded_io",
  "//external/log/v0.4.22:log",
  "//external/pinned-init/v0.0.8:pinned_init",
  "//external/spin/v0.9.8:spin",
  "//external/thiserror/v2.0.9:thiserror",
  "//external/tock-registers/v0.9.0:tock_registers",
  "//kernel/arch:bluekernel_arch",
  "//kernel/header:bluekernel_header",
  "//kernel/infra:bluekernel_infra",
  "//kernel/kconfig:bluekernel_kconfig",
  "//libc:libc",
]

shared_features = [
  "tlsf",
  "overflow_check",
  "idle_hook",
  "heap",
  "heap_isr",
  "debugging_init",
  "event",
  "messagequeue",
  "mailbox",
  "mutex",
  "semaphore",
  "rwlock",
  "condvar",
  "thread_priority_max",
  "schedule_with_time_slice",
  "stack_highwater_check",
]

shared_rust_build_flags = []
if (direct_syscall_handler) {
  shared_rust_build_flags += [
    "--cfg",
    "direct_syscall_handler",
  ]
}
if (bsp == "qemu_mps2_an385" || bsp == "qemu_mps3_an547") {
  shared_rust_build_flags += [
    "--cfg",
    "hardware_schedule",
  ]
}

shared_rust_build_flags += [
  "--cfg",
  "target_board=\"$bsp\"",
]

build_rust("kernel_unittest") {
  testonly = true
  crate_name = "kernel_unittest"
  crate_type = "bin"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  proc_macro_deps = [
    "//external/paste/v1.0.15:paste",
    "//external/rust-delegate/v0.13.3:delegate",
    "//kernel/test_harness:bluekernel_test_macro",
  ]
  deps = shared_deps
  features = shared_features

  inputs = [ "//kernel/kernel/src/boards/$bsp/link.x" ]
  rustflags = shared_rust_build_flags
  rustflags += test_image_rustflags
  rustflags += common_crate_rustflags
}

build_rust("bluekernel") {
  crate_type = "rlib"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  proc_macro_deps = [
    "//external/paste/v1.0.15:paste",
    "//external/rust-delegate/v0.13.3:delegate",
  ]
  deps = shared_deps
  features = shared_features

  rustflags = shared_rust_build_flags
  rustflags += common_crate_rustflags
}

build_rust("bluekernel_std_static") {
  crate_name = "bluekernel"
  crate_type = "staticlib"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  proc_macro_deps = [
    "//external/paste/v1.0.15:paste",
    "//external/rust-delegate/v0.13.3:delegate",
  ]
  deps = shared_deps
  features = shared_features
  features += [ "std" ]
  rustflags = shared_rust_build_flags
  rustflags += common_crate_rustflags
}

build_rust("bluekernel_std") {
  crate_name = "bluekernel"
  crate_type = "rlib"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  proc_macro_deps = [
    "//external/paste/v1.0.15:paste",
    "//external/rust-delegate/v0.13.3:delegate",
  ]
  deps = shared_deps
  features = shared_features
  features += [ "std" ]
  rustflags = shared_rust_build_flags
  rustflags += common_crate_rustflags
}

build_rust("bluekernel_posixtestsuite") {
  testonly = true
  crate_name = "bluekernel"
  crate_type = "rlib"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  proc_macro_deps = [
    "//external/paste/v1.0.15:paste",
    "//external/rust-delegate/v0.13.3:delegate",
  ]
  deps = shared_deps
  features = shared_features
  features += [ "posixtestsuite" ]
  rustflags = shared_rust_build_flags
  rustflags += common_crate_rustflags
}

cbindgen("rust_wrapper") {
  sources = [ "cbindgen.toml" ]
  args = [
           "--quiet",
           "-c",
         ] + rebase_path([ "cbindgen.toml" ], root_build_dir) +
         rebase_path([ "." ], root_build_dir)
}

gen_qemu_runner("integration_test_runner") {
  testonly = true
  img = ":kernel_integration_test"
  qemu = "$qemu_exe"
  board = "$bsp"
}

run_qemu_check("run_integration_test") {
  testonly = true
  runner = ":integration_test_runner"
  checker = "tests/integration_test.rs"
}

gen_qemu_runner("unittest_runner") {
  testonly = true
  img = ":kernel_unittest"
  qemu = "$qemu_exe"
  board = "$bsp"
}

run_qemu_check("run_unittest") {
  testonly = true
  runner = ":unittest_runner"
  checker = "src/lib.rs"
}
