import("//build/templates/build_template.gni")
import("//build/toolchain/bluekernel.gni")
import("//kernel/common_crate_rustflags.gni")

build_rust("kernel_integration_test") {
  testonly = true
  crate_type = "bin"
  sources = [ "tests/integration_test.rs" ]
  edition = "2021"
  proc_macro_deps = [ "//kernel/test_harness:bluekernel_test_macro" ]
  deps = [
    ":bluekernel",
    "//kernel/scal:bluekernel_scal_test",
    "//libc:libc",
  ]

  inputs = [ "//kernel/kernel/src/bsp/$bsp/link.x" ]
  rustflags = test_image_rustflags
  rustflags += common_crate_rustflags
}

shares_deps = [
  "//external/bitflags/v2.9.0:bitflags",
  "//external/cfg-if/v1.0.0:cfg_if",
  "//external/const-default/v1.0.0:const_default",
  "//external/cortex-m:cortex_m",
  "//external/embedded-io/v0.6.1:embedded_io",
  "//external/log/v0.4.22:log",
  "//external/pinned-init/v0.0.8:pinned_init",
  "//external/spin/v0.9.8:spin",
  "//external/thiserror/v2.0.9:thiserror",
  "//external/tock-registers/v0.9.0:tock_registers",
  "//kernel/arch:bluekernel_arch",
  "//kernel/header:bluekernel_header",
  "//kernel/infra:bluekernel_infra",
  "//kernel/kconfig:bluekernel_kconfig",
  "//libc:libc",
]

share_features = [
  "tlsf",
  "overflow_check",
  "idle_hook",
  "heap",
  "heap_isr",
  "debugging_init",
  "event",
  "messagequeue",
  "mailbox",
  "mutex",
  "semaphore",
  "rwlock",
  "condvar",
  "thread_priority_max",
  "schedule_with_time_slice",
  "stack_highwater_check",
]

share_rust_build_flags = []
if (direct_syscall_handler) {
  share_rust_build_flags += [
    "--cfg",
    "direct_syscall_handler",
  ]
}
if (bsp == "qemu_mps2_an385" || bsp == "qemu_mps3_an547") {
  share_rust_build_flags += [
    "--cfg",
    "hardware_schedule",
  ]
}

share_rust_build_flags += [
  "--cfg",
  "target_board=\"$bsp\"",
]

build_rust("kernel_unit_test") {
  testonly = true
  crate_name = "kernel_unit_test"
  crate_type = "bin"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  proc_macro_deps = [
    "//external/paste/v1.0.15:paste",
    "//external/rust-delegate/v0.13.3:delegate",
    "//kernel/test_harness:bluekernel_test_macro",
  ]
  deps = shares_deps
  features = share_features

  inputs = [ "//kernel/kernel/src/bsp/$bsp/link.x" ]
  rustflags = share_rust_build_flags
  rustflags += test_image_rustflags
  rustflags += common_crate_rustflags
}

build_rust("bluekernel") {
  crate_type = "rlib"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  proc_macro_deps = [
    "//external/paste/v1.0.15:paste",
    "//external/rust-delegate/v0.13.3:delegate",
  ]
  deps = shares_deps
  features = share_features

  rustflags = share_rust_build_flags
  rustflags += common_crate_rustflags
}

cbindgen("rust_wrapper") {
  sources = [ "cbindgen.toml" ]
  args = [
           "--quiet",
           "-c",
         ] + rebase_path([ "cbindgen.toml" ], root_build_dir) +
         rebase_path([ "." ], root_build_dir)
}

gen_qemu_runner("integration_test_runner") {
  testonly = true
  img = ":kernel_integration_test"
  qemu = "$qemu_exe"
  board = "$bsp"
}

run_qemu_check("run_integration_test") {
  testonly = true
  runner = ":integration_test_runner"
  checker = "tests/integration_test.rs"
}

gen_qemu_runner("unit_test_runner") {
  testonly = true
  img = ":kernel_unit_test"
  qemu = "$qemu_exe"
  board = "$bsp"
}

run_qemu_check("run_unit_test") {
  testonly = true
  runner = ":unit_test_runner"
  checker = "src/lib.rs"
}
