import("//build/templates/build_template.gni")

action("kconfig_const") {
  script = "//kernel/kconfig/src/parse_const.py"
  inputs = [
    "config/Kconfig",
    "config/$board/$build_type/defconfig",
  ]
  output = rebase_path("$target_gen_dir/kconfig.rs")
  outputs = [ "$target_gen_dir/kconfig.rs" ]
  args = [
    "--kconfig",
    rebase_path("config/Kconfig"),
    "--board",
    "$board",
    "--build_type",
    "$build_type",
    "--output",
    "$output",
  ]
}

build_rust("bluekernel_kconfig") {
  crate_type = "rlib"
  sources = get_target_outputs(":kconfig_const")
  edition = "2021"
  deps = [ ":kconfig_const" ]
}

config("kconfigs") {
  args = [
    "--kconfig",
    rebase_path("config/Kconfig"),
    "--board",
    "$board",
    "--build_type",
    "$build_type",
  ]
  parse_configs = rebase_path("//kernel/kconfig/src/parse_rustflags.py")
  kernel_configs = exec_script(parse_configs, args, "list lines")
  rustflags = []
  foreach(_config, kernel_configs) {
    rustflags += [
      "--cfg",
      _config,
    ]
  }
}
