import("//build/templates/build_template.gni")

bindgen("bsp_bindgen") {
  deps = [ "//kernel/kernel:rust_wrapper" ]
  bindgen_flags = [
    "--use-core",
    "--ctypes-prefix",
    "core::ffi",
  ]
  sources = [ "include/rt_wrapper.h" ]
  include_dirs = [
    "//rt-dev/components/drivers/include",
    "//kernel/adapter/rt_thread/include",
    "//rt-dev/include/",
    "//rt-dev/components/finsh",
    "//rt-dev/bsp/$bsp",
  ]
  inputs = [
    "//rt-dev/include/rtdef.h",
    "//rt-dev/components/drivers/include/rtdevice.h",
    "//rt-dev/include/rtthread.h",
  ]
  cflags = [
    "--target=$llvm_target",
    "-fshort-enums",
    "-fsigned-char",
  ]
}

build_rust("bluekernel_bsp") {
  crate_type = "staticlib"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  features = [
    "enable_uart0",
    "enable_uart1",
  ]
  deps = [
    ":bsp_bindgen",
    "//external/cortex-m:cortex_m",
    "//external/embedded-hal/v1.0.0:embedded_hal",
    "//external/embedded-io/v0.6.1:embedded_io",
    "//external/thiserror/v2.0.9:thiserror",
    "//external/tock-registers/v0.9.0:tock_registers",
    "//kernel/arch:bluekernel_arch",
    "//kernel/kernel:bluekernel",
  ]

  if (bsp == "qemu-mps2-an385") {
    rustflags = [
      "--cfg",
      "target_board=\"qemu_mps2_an385\"",
    ]
  } else if (bsp == "qemu-mps3-an547") {
    rustflags = [
      "--cfg",
      "target_board=\"qemu_mps3_an547\"",
    ]
  }

  output_file = get_target_outputs(":bsp_bindgen")
  rustenv = [ "BINDGEN_DIR=" + rebase_path(output_file[0]) ]
}
